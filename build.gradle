buildscript {
    ext {
        springBootVersion = '1.2.6.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://github.com/paulsmelser/maven-repo/raw/master/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'oozie-console'
    version = '1.0.0-SNAPSHOT'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    maven{
        url "https://github.com/paulsmelser/maven-repo/raw/master/"
    }
}
loadConfiguration()

mainClassName = "com.smelser.oozie.Application"

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile project(":oozie-rest-client")
    compile project(":oozie-web")
    compile project(":oozie-application")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
        maven{
                url "https://github.com/paulsmelser/maven-repo/raw/master/"
        }
    }

    dependencies{
        compile("com.thoughtworks.xstream:xstream:1.4.7")
        compile("com.sun.jersey:jersey-client:1.18.3")
        compile("com.smelser:SimpleMapper:0.0.1")
        compile("com.owlike:genson:0.99")
        compile("com.google.code.gson:gson:2.3.1")
        compile("org.springframework.boot:spring-boot-starter-thymeleaf")
        testCompile("junit:junit")
        testCompile("org.mockito:mockito-core")
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }
}

bootRun {
    main = 'com.smelser.oozie.Application'
    systemProperties = [
            'spring.profiles.active': "$config.spring.profiles.active"
    ]
}

subprojects {
    group = 'com.silanis.esl'
    version = '1.0.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

def loadConfiguration() {
    def environment = hasProperty('env') ? env : 'dev'
    System.setProperty('environment', environment)
    println "Environment is set to $environment"

    def configFile = file('config.groovy')
    def config = new ConfigSlurper(environment).parse(configFile.toURL())
    extensions.add('config', config)
}
